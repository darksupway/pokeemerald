script MysteryManMysticTicket {
    lockall
    if ((flag(FLAG_BATTLED_DEOXYS) || flag(FLAG_DEFEATED_DEOXYS)) &&
        (flag(FLAG_CAUGHT_MEW) || flag(FLAG_DEFEATED_MEW)) &&
        (flag(FLAG_CAUGHT_LATIAS_OR_LATIOS) || flag(FLAG_DEFEATED_LATIAS_OR_LATIOS)))
    {
        goto(MysteryManMysticTicket_DefeatedOtherLegendaries)
    } else {
        goto(MysteryManMysticTicket_ComeBackAfterTickets)
    }
}

script MysteryManMysticTicket_DefeatedOtherLegendaries {
    faceplayer

    if (!flag(FLAG_MYSTERY_MAN_BAG_FULL)) {
        msgbox(MysteryManMysticTicket_Text, MSGBOX_DEFAULT)
    } else {
        msgbox(gText_MysteryMan_Return, MSGBOX_DEFAULT)
    }
    
    checkitem(ITEM_MYSTIC_TICKET)
    if (var(VAR_RESULT) == TRUE) {
        msgbox(MysteryManMysticTicket_HasTicketAlready, MSGBOX_DEFAULT)
        msgbox(MysteryManMysticTicket_Obtained_Text, MSGBOX_AUTOCLOSE)
    } else {
        checkitemspace(ITEM_MYSTIC_TICKET)
        if (var(VAR_RESULT) == FALSE) {
            goto(Common_EventScript_MysteryMan_NoBagSpace)
        }

        giveitem(ITEM_MYSTIC_TICKET)
        setflag(FLAG_ENABLE_SHIP_NAVEL_ROCK)
        setflag(FLAG_RECEIVED_MYSTIC_TICKET)
        
        msgbox(MysteryManMysticTicket_Obtained_Text, MSGBOX_AUTOCLOSE)
    }

    clearflag(FLAG_MYSTERY_MAN_BAG_FULL)

    fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(VAR_LAST_TALKED)
	fadescreenswapbuffers(FADE_FROM_BLACK)

    setflag(FLAG_HIDE_MYSTERY_MAN_ABOVE_BATTLE_DOME)
    clearflag(FLAG_HIDE_MYSTERY_MAN_FORGOTTEN_CAVE)
    setvar(VAR_MYSTERY_MAN_STATE, 5)

    releaseall
    end
}

script MysteryManMysticTicket_ComeBackAfterTickets {
    msgbox(MysteryManMysticTicket_ComeBackAfterTickets_Text, MSGBOX_AUTOCLOSE)
    releaseall
    end
}

text MysteryManMysticTicket_Text {
    "Ah, you are here.\n"
    "...\p"
    "So what do you think after\n"
    "becoming a Champion and meeting\l"
    "all sorts of Pokemon?\p"
    "Are you satisfied? Or are you\n"
    "searching for more challenges?\p"
    "Here, the last ticket I have for\n"
    "you."
}

text MysteryManMysticTicket_ComeBackAfterTickets_Text {
    "Hmm...\p"
    "It seems you have neither caught\n"
    "nor defeated the Pokemon on\l"
    "the islands.\p"
    "Come back after doing that."
}

text MysteryManMysticTicket_Obtained_Text {
    "After you are done with this\n"
    "one, please come meet me in the\l"
    "FORGOTTEN CAVE located on the\l"
    "east side of the Frontier\p"
    "If you have not found it yet...\n"
    "Well, I don't think you will have\l"
    "a problem finding it."
}

text MysteryManMysticTicket_HasTicketAlready {
    "Oh... It seems you already have\n"
    "a MYSTIC TICKET.\p"
    "In that case, I'll just go to the\n"
    "next location."
}
